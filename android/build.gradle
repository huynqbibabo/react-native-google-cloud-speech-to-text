buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.get('kotlinVersion') : project.properties['GoogleCloudSpeechToText_kotlinVersion']

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:3.4.1"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.9"
  }
}

ext {
  grpcVersion = '1.4.0'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['GoogleCloudSpeechToText_' + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['GoogleCloudSpeechToText_' + name]).toInteger()
}

android {
  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')
  buildToolsVersion getExtOrDefault('buildToolsVersion')
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
    versionCode 1
    versionName "1.0"

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.3.0'
  }
  plugins {
    javalite {
      artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
    }
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.plugins {
        javalite {}
        grpc {
          // Options added to --grpc_out
          option 'lite'
        }
      }
    }
  }
}

repositories {
  mavenCentral()
  jcenter()
  google()

  def found = false
  def defaultDir = null
  def androidSourcesName = 'React Native sources'

  if (rootProject.ext.has('reactNativeAndroidRoot')) {
    defaultDir = rootProject.ext.get('reactNativeAndroidRoot')
  } else {
    defaultDir = new File(
            projectDir,
            '/../../../node_modules/react-native/android'
    )
  }

  if (defaultDir.exists()) {
    maven {
      url defaultDir.toString()
      name androidSourcesName
    }

    logger.info(":${project.name}:reactNativeAndroidRoot ${defaultDir.canonicalPath}")
    found = true
  } else {
    def parentDir = rootProject.projectDir

    1.upto(5, {
      if (found) return true
      parentDir = parentDir.parentFile

      def androidSourcesDir = new File(
              parentDir,
              'node_modules/react-native'
      )

      def androidPrebuiltBinaryDir = new File(
              parentDir,
              'node_modules/react-native/android'
      )

      if (androidPrebuiltBinaryDir.exists()) {
        maven {
          url androidPrebuiltBinaryDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidPrebuiltBinaryDir.canonicalPath}")
        found = true
      } else if (androidSourcesDir.exists()) {
        maven {
          url androidSourcesDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidSourcesDir.canonicalPath}")
        found = true
      }
    })
  }

  if (!found) {
    throw new GradleException(
            "${project.name}: unable to locate React Native android sources. " +
                    "Ensure you have you installed React Native as a dependency in your project and try again."
    )
  }
}

def kotlin_version = getExtOrDefault('kotlinVersion')

dependencies {
  // noinspection GradleDynamicVersion
  api 'com.facebook.react:react-native:+'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // gRPC
  implementation "io.grpc:grpc-okhttp:$grpcVersion"
  implementation "io.grpc:grpc-protobuf-lite:$grpcVersion"
  implementation "io.grpc:grpc-stub:$grpcVersion"

  // Annotation API
  implementation 'javax.annotation:javax.annotation-api:1.3.2'

  // Protobuf
  protobuf 'com.google.protobuf:protobuf-java:3.14.0'

  // OAuth2 for Google API
  implementation('com.google.auth:google-auth-library-oauth2-http:0.22.0') {
    exclude module: 'httpclient'
  }

  //RxJava
  implementation "io.reactivex.rxjava2:rxjava:2.2.10"
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
}

task copySecretKey(type: Copy) {
  def File secretKey = file "$System.env.GOOGLE_APPLICATION_CREDENTIALS"
  from secretKey.getParent()
  include secretKey.getName()
  into 'src/main/res/raw'
  rename secretKey.getName(), "credential.json"
}
preBuild.dependsOn(copySecretKey)
